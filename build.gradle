allprojects {
	buildscript {
		repositories { mavenCentral(); jcenter() }
	}
	repositories { mavenCentral() }

	apply plugin: 'eclipse'

	// Always generate clean eclipse files.
	tasks.eclipse.dependsOn tasks.cleanEclipse
}

subprojects {
	// Use Java 8 for all projects
	apply plugin: 'java'
	sourceCompatibility = 1.8

	dependencies {
		compile(
			'com.google.guava:guava:18.0'
		)
		testCompile(
			'junit:junit:4.12',
			'org.hamcrest:hamcrest-all:1.3'
		)
	}
	
	javadoc {
		failOnError = false
	}
}


task javadocAll(type: Javadoc) {
	description 'Collect JavaDoc from all sub modules'
	source subprojects.collect { project ->
		project.sourceSets.main.allJava
	}
	classpath = files(subprojects.collect { project ->
		project.sourceSets.main.compileClasspath
	})
	destinationDir = file("$buildDir/docs/javadoc")
}